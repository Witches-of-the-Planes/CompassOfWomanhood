//
// Shithri-Neera romance scripts, Shithri's part
//


// If "comitted" and Neera is enemy, so is Shithri

IF
  InParty(Myself)
  Allegiance("Neera",ENEMY)
  Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    LeaveParty()
    Enemy()
END

IF
  !InParty(Myself)
  Allegiance("6WSHITHRI",ENEMY)
  !Allegiance(Myself,ENEMY)
  Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    ChangeAIScript("",SPECIFICS)
    Enemy()
END


// If Neera non-perma dies while "comitted",
// Shithri cries and after the fight is over ---
// demands to raise Neera or will leave the party
// (alongside with dead Neera)
IF
  Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
  Global("6W#ShithriNeeraNeeraDied","GLOBAL",0)
  InPartyAllowDead("Neera")
  Dead("Neera")
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("6W#ShithriNeeraNeeraDied","GLOBAL",1)
    Interact(Player1)
    SetGlobal("6W#ShithriNeeraNeeraDead","GLOBAL",1)
    SetInterrupt(TRUE)
END

IF
  Global("6W#ShithriNeeraNeeraDead","GLOBAL",1)
  OR(2)
    Global("6W#ShithriNeeraNeeraDeadRaise","GLOBAL",0) // never raised
    Global("6W#ShithriNeeraNeeraDeadRaise","GLOBAL",4) // raised somewhen already
  !ActuallyInCombat()
  !See([Enemy])
THEN
  RESPONSE #100
    Wait(1)
    SetInterrupt(FALSE)
    SetGlobal("6W#ShithriNeeraNeeraRaise","GLOBAL",1)
    Interact(Player1) // sets to 2, then 3 will mean she's already raised (then to 4)
    SetInterrupt(TRUE)
END

IF
  Global("6W#ShithriNeeraNeeraRaise","GLOBAL",2)
  InParty("Neera")
  !Dead("Neera")
THEN
  RESPONSE #100
    //TODO
END

// If Neera just died and is outside of the party,
// she probably perma-died, e.g. disintegrated or massacred.
// Force "Neera died" dialogue even if it was run before.
//TODO: should Shithri leave the party or sth?
IF
  Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
  !InPartyAllowDead("Neera")
  Dead("Neera")
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("6W#ShithriNeeraNeeraDead","GLOBAL",1)
    Interact(Player1)
    SetGlobal("6W#ShithriNeeraNeeraDied","GLOBAL",2)
    SetInterrupt(TRUE)
END

/*
// If Neera dies permanently, the romance ends
IF
  OR(2)
    Global("6W#ShithriNeeraRomanceActive","GLOBAL",1)
    Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
  !InPartyAllowDead("Neera")
  Dead("Neera")
THEN
  RESPONSE #100
    SetGlobal("6W#ShithriNeeraRomanceActive","GLOBAL",3)
    SetGlobal("6W#ShithriNeeraNeeraDead","GLOBAL",3)
END
*/


//
// Each time Neera will leave the party, Shithri should do so too.
//
// If some case is unhandled though - e.g. Neera is kicked out
// but the PC runs away so that she can't have her unjoin dialogue,
// Shithri should leave too.
//
// Same if Shithri is unconscious, asleep etc.
//

IF
  Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
  InParty(Myself)
  !Dead("6WSHITHRI") // `Dead` cannot accept Myself
  !InPartyAllowDead("Neera")
  Global("6W#ShithriMoveToNeera","GLOBAL",0)
  // Unless Neera leaves, she can still talk to PC
  OR(2)
    !InMyArea("Neera")
    Global("6W#NeeraWaitingHere","GLOBAL",1)
THEN
  RESPONSE #100
    // set global so that filtering is less repetitive
    SetInterrupt(FALSE)
    LeaveParty()
    ChangeAIScript("6WSHITP",DEFAULT)
    ActionOverride("Neera", ChangeAIScript("6WNEERP",DEFAULT))
    SetGlobal("KickedOut","LOCALS",1)
    SetGlobal("6W#ShithriMoveToNeera","GLOBAL",1)
    SetInterrupt(TRUE)
    Continue()
END


// Start the romance

IF
  InParty(Myself)
  InParty("Neera")
  Global("6W#ShithriNeeraRomanceActive","GLOBAL",0)
  !Global("NeeraRomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("6W#ShithriNeeraRomanceActive","GLOBAL",1)
    SetGlobal("6W#ShithriNeeraLoveTalk","GLOBAL",1)
    RealSetGlobalTimer("6W#ShithriNeeraRomance","GLOBAL",TWO_HOURS)
END


// Shithri starts the following dialogues:
IF
  OR(6)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",6)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",8)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",14)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",16)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",20)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",26)
THEN
  RESPONSE #100
    Interact("Neera")
END


// Lovetalk no 0 cannot happen in a tavern or on the sea,
// as it would make no sense.
IF
  Global("6W#ShithriNeeraLoveTalk","GLOBAL",1)
  OR(2)
    Global("6W#ShithriNeeraRomanceActive","GLOBAL",1)
    Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
  IfValidForPartyDialog("6WSHITHRI")
  IfValidForPartyDialog("Neera")
  See("Neera")
  CombatCounter(0)
  !See([ENEMY])
  !Global("Chapter","GLOBAL",4) // Brynnlaw, Spellhold etc
  !AreaCheck("AR0021")  // Crooked Crane 1st Floor
  !AreaCheck("AR0313")  // Sea Bounty Tavern 1st Floor
  !AreaCheck("AR0406")  // Copper Coronet
  !AreaCheck("AR0509")  // Five Flagons Inn 1st Floor
  !AreaCheck("AR0513")  // Calbor's Inn at Bridge District 1st Floor
  !AreaCheck("AR0522")  // Five Flagons Inn (Stronghold)
  !AreaCheck("AR0704")  // Mithrest Inn
  !AreaCheck("AR0709")  // Den of the Seven Vales 1st Floor
  !AreaCheck("AR1105")  // Imnesvale Inn
  !AreaCheck("AR1602")  // Brynnlaw's Inn
  !AreaCheck("AR2010")  // Trademeet's Inn
  RealGlobalTimerExpired("6W#ShithriNeeraRomance","GLOBAL")
THEN
  RESPONSE #100
    IncrementGlobal("6W#ShithriNeeraLoveTalk","GLOBAL",1)
    RealSetGlobalTimer("6W#ShithriNeeraRomance","GLOBAL",FIVE_HOURS)
END

// Lovetalk no 4 can only be triggered at night in a forest.
IF
  Global("6W#ShithriNeeraLoveTalk","GLOBAL",9)
  OR(2)
    Global("6W#ShithriNeeraRomanceActive","GLOBAL",1)
    Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
  IfValidForPartyDialog("6WSHITHRI")
  IfValidForPartyDialog("Neera")
  See("Neera")
  CombatCounter(0)
  !See([ENEMY])
  AreaType(FOREST)
  TimeOfDay(NIGHT)
  RealGlobalTimerExpired("6W#ShithriNeeraRomance","GLOBAL")
THEN
  RESPONSE #100
    IncrementGlobal("6W#ShithriNeeraLoveTalk","GLOBAL",1)
    RealSetGlobalTimer("6W#ShithriNeeraRomance","GLOBAL",FIVE_HOURS)
END

// Lovetalk no 6 can only be triggered in a tavern
IF
  Global("6W#ShithriNeeraLoveTalk","GLOBAL",13)
  OR(2)
    Global("6W#ShithriNeeraRomanceActive","GLOBAL",1)
    Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
  IfValidForPartyDialog("6WSHITHRI")
  IfValidForPartyDialog("Neera")
  See("Neera")
  CombatCounter(0)
  !See([ENEMY])
  OR(11)
    AreaCheck("AR0021") // Crooked Crane 1st Floor
    AreaCheck("AR0313") // Sea Bounty Tavern 1st Floor
    AreaCheck("AR0406") // Copper Coronet
    AreaCheck("AR0509") // Five Flagons Inn 1st Floor ?
    AreaCheck("AR0513") // Calbor's Inn at Bridge District 1st Floor
    AreaCheck("AR0522") // Five Flagons Inn (Stronghold) ?
    AreaCheck("AR0704") // Mithrest Inn
    AreaCheck("AR0709") // Den of the Seven Vales 1st Floor
    AreaCheck("AR1105") // Imnesvale Inn
    AreaCheck("AR1602") // Brynnlaw's Inn (The Vulgar Monkey)
    AreaCheck("AR2010") // Trademeet's Inn (Vyatri's Pub)
  RealGlobalTimerExpired("6W#ShithriNeeraRomance","GLOBAL")
THEN
  RESPONSE #100
    IncrementGlobal("6W#ShithriNeeraLoveTalk","GLOBAL",1)
    RealSetGlobalTimer("6W#ShithriNeeraRomance","GLOBAL",FIVE_HOURS)
END

// Other dialogues are fine
IF
  RealGlobalTimerExpired("6W#ShithriNeeraRomance","GLOBAL")
  IfValidForPartyDialog("6WSHITHRI")
  IfValidForPartyDialog("Neera")
  See("Neera")
  CombatCounter(0)
  OR(2)
    Global("6W#ShithriNeeraRomanceActive","GLOBAL",1)
    Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
  OR(6)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",3)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",5)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",7)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",11)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",15)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",17)
THEN
  RESPONSE #100
    IncrementGlobal("6W#ShithriNeeraLoveTalk","GLOBAL",1)
    RealSetGlobalTimer("6W#ShithriNeeraRomance","GLOBAL",FIVE_HOURS)
END

//
// Some dialogues are only after "commitment"
//

// Lovetalk no 13 can only be triggered while not in a forest.
IF
  Global("6W#ShithriNeeraLoveTalk","GLOBAL",25)
  Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
  IfValidForPartyDialog("6WSHITHRI")
  IfValidForPartyDialog("Neera")
  See("Neera")
  CombatCounter(0)
  !See([ENEMY])
  !AreaType(FOREST)
  !AreaCheck("AR1900") /* Druid Grove */
  RealGlobalTimerExpired("6W#ShithriNeeraRomance","GLOBAL")
THEN
  RESPONSE #100
    IncrementGlobal("6W#ShithriNeeraLoveTalk","GLOBAL",1)
    RealSetGlobalTimer("6W#ShithriNeeraRomance","GLOBAL",FIVE_HOURS)
END

IF
  RealGlobalTimerExpired("6W#ShithriNeeraRomance","GLOBAL")
  IfValidForPartyDialog("6WSHITHRI")
  IfValidForPartyDialog("Neera")
  See("Neera")
  CombatCounter(0)
  Global("6W#ShithriNeeraRomanceActive","GLOBAL",2)
  OR(4)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",19)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",21)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",23)
    Global("6W#ShithriNeeraLoveTalk","GLOBAL",25)
THEN
  RESPONSE #100
    IncrementGlobal("6W#ShithriNeeraLoveTalk","GLOBAL",1)
    RealSetGlobalTimer("6W#ShithriNeeraRomance","GLOBAL",FIVE_HOURS)
END

//
// Neera mentions Reginald once. Shithri may talk to him,
// but she may just meet (i.e. detect) him too.
//
// Could filter talking to him, but that seems overly complex
// for a really minor benefit.
//
IF
  Global("6W#ShithriReginald","GLOBAL",0)
  Detect("OHNREGI")
THEN
  RESPONSE #100
    SetGlobal("6W#ShithriReginald","GLOBAL",1)
END
