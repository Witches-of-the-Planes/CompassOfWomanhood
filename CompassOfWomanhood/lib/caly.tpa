
//
// Dialogues
//

ACTION_DEFINE_ARRAY dialogues
BEGIN
  // SoA
  ~CompassOfWomanhood/dialogues/caly/6WCAL0.d~
  ~CompassOfWomanhood/dialogues/caly/6WCALY.d~
  //~CompassOfWomanhood/dialogues/caly/B6WCALY.d~
  //~CompassOfWomanhood/dialogues/caly/6WCALYP.d~
  //~CompassOfWomanhood/dialogues/caly/6WCALYJ.d~
  //~CompassOfWomanhood/dialogues/caly/6WCALYD.d~

  // ToB
  //~CompassOfWomanhood/dialogues/caly/6WCAL25.d~
  //~CompassOfWomanhood/dialogues/caly/6WCAL25A.d~
  //~CompassOfWomanhood/dialogues/caly/B6WCAL25.d~
  //~CompassOfWomanhood/dialogues/caly/6WCAL25P.d~
  //~CompassOfWomanhood/dialogues/caly/6WCAL25J.d~
  //~CompassOfWomanhood/dialogues/caly/6WCAL25D.d~
END

ACTION_DEFINE_ARRAY dialogues_extra
BEGIN
  // interjections
  //~CompassOfWomanhood/dialogues/caly/caly_interjections.d~

  // quests
  ~CompassOfWomanhood/dialogues/caly/caly_tales.d~
END

SILENT
OUTER_SET file_num = 0

ACTION_PHP_EACH dialogues AS num => dialog
BEGIN
  COMPILE ~%dialog%~ ~override~
  OUTER_SET file_num += 1
END

ACTION_PHP_EACH dialogues_extra AS num => dialog
BEGIN
  COMPILE ~%dialog%~ ~override~
  OUTER_SET file_num += 1
END

PRINT "Compile %file_num% dialog files"


//
// Game files
//

SILENT
OUTER_SET file_num = 0

APPEND ~pdialog.2da~
  ~6WCALY       6WCALYP            6WCALYJ           6WCALYD~
  UNLESS ~6WCALY~
  UNLESS ~25POST~

APPEND ~pdialog.2da~
  ~6WCALY       6WCALYP            6WCALYJ           6WCALYD        6WCAL25P           6WCAL25J              6WCAL25D              6WCAL25~
  UNLESS ~6WCALY~
  IF ~25POST~
OUTER_SET file_num += 1

APPEND ~interdia.2da~
  ~6WCALY      B6WCALY~
  UNLESS ~6WCALY~
  UNLESS ~25FILE~

APPEND ~interdia.2da~
  ~6WCALY      B6WCALY       B6WCAL25~
  UNLESS ~6WCALY~
  IF ~25FILE~
OUTER_SET file_num += 1

APPEND ~npclevel.2da~
  ~6WCALY    6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08  6WCALY086 WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08  6WCALY086 WCALY08 6WCALY08 6WCALY086 WCALY08 6WCALY08 WCALY08~
  UNLESS ~6WCALY~
OUTER_SET file_num += 1

APPEND ~npclvl25.2da~
  ~6WCALY    6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08  6WCALY086 WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08   6WCALY08  6WCALY08 6WCALY08 6WCALY08 6WCALY08 6WCALY08 6WCALY08 6WCALY08~
  UNLESS ~6WCALY~
OUTER_SET file_num += 1


//
// Sounds
//

// `LAF HANDLE_AUDIO` doesn't seem to support %LANGUAGE% placeholder
/*
LAF HANDLE_AUDIO
  STR_VAR
    oggdec_path = ~CompassOfWomanhood/sounds~
    audio_path  = ~CompassOfWomanhood/translations/%LANGUAGE%/sounds/caly~
END
*/

// US English is the base
SILENT
OUTER_SET file_num = 0
ACTION_BASH_FOR ~CompassOfWomanhood/translations/en_US/sounds/caly~ ~^B?6W.*\.wav$~
BEGIN
  COPY ~%BASH_FOR_FILESPEC%~ ~override~
  OUTER_SET file_num += 1
END
PRINT "Copied %file_num% sound files for en_US"

// Other languages may add or overwrite some sounds.
ACTION_IF !(%LANGUAGE% STRING_EQUAL "en_US") THEN
BEGIN
  SILENT
  OUTER_SET file_num = 0
  ACTION_BASH_FOR ~CompassOfWomanhood/translations/%LANGUAGE%/sounds/caly~ ~^B?6W.*\.wav$~
  BEGIN
    COPY ~%BASH_FOR_FILESPEC%~ ~override~
    OUTER_SET file_num += 1
  END
  PRINT "Copied %file_num% sound files for %LANGUAGE%"
END


//
// Companion herself
//

// portraits
SILENT
COPY ~CompassOfWomanhood/creatures/caly/6WcalyS.bmp~ ~override~
COPY ~CompassOfWomanhood/creatures/caly/6WcalyM.bmp~ ~override~
COPY ~CompassOfWomanhood/creatures/caly/6WcalyL.bmp~ ~override~
PRINT "Copied 3 portrait files"

// creature
SILENT
OUTER_SET file_num = 0
OUTER_SET file_edited_num = 0

// Need variables for Caly's name strings, to use them in the scripts later on.
OUTER_SET 6WCALY_NAME_0 = RESOLVE_STR_REF(@10)
OUTER_SET 6WCALY_NAME_1 = RESOLVE_STR_REF(@11)
OUTER_SET 6WCALY_NAME_2 = RESOLVE_STR_REF(@12)
OUTER_SET 6WCALY_TALES_BUY = RESOLVE_STR_REF(@99)

COPY ~CompassOfWomanhood/creatures/caly/6WCALY00.cre~ ~override~
  WRITE_LONG NAME1 6WCALY_NAME_0
  WRITE_LONG NAME2 6WCALY_NAME_0

  SAY BATTLE_CRY1 @109 /* Taste my whip! */
  // SAY HURT  @110 /* That wasn't nice! */
  // SAY DYING @111 /* So this is the "big death"... */
OUTER_SET file_num += 1

ACTION_FOR_EACH cre_file IN ~6WCALY08~ // ~6WCALY09~ ~6WCALY10~ ~6WCALY11~ ~6WCALY13~ ~6WCALY15~ ~6WCALY25~
BEGIN
  COPY ~CompassOfWomanhood/creatures/caly/%cre_file%.cre~ ~override~
    WRITE_LONG NAME1 6WCALY_NAME_1
    WRITE_LONG NAME2 6WCALY_NAME_2
    SAY BIO @13
  
    SAY HAPPY                  @100 /* We're good together, aren't we? */
    SAY UNHAPPY_ANNOYED        @101 /* I'm starting to feel a bit... dissapointed? */
    SAY UNHAPPY_SERIOUS        @102 /* We have to rethink our ways. */
    SAY UNHAPPY_BREAKING_POINT @103 /* How dare you stoop so low! */
    SAY LEADER                 @104 /* Ready to lead you all to the brightest future! */
    SAY TIRED                  @105 /* We've been going hard for too long. */
    SAY BORED                  @106 /* C'mon, this girl just wanna have fuuun. */
    SAY BATTLE_CRY1            @107 /* You have a spank wish! */
    SAY BATTLE_CRY2            @108 /* Prepare to feel a lot of pain... and a tiny bit of pleasure */
    SAY BATTLE_CRY3            @109 /* Taste my whip! */
    SAY HURT                   @110 /* That wasn't nice! */
    SAY DYING                  @111 /* So this is the "big death"... */
    SAY AREA_FOREST            @112 /* Ouch, muddy, ouch, wet, ouch INSECTS! */
    SAY AREA_CITY              @113 /* Finally, it feels like home! */
    SAY AREA_DUNGEON           @114 /* Ouch, muddy, ouch, wet, ouch RODENTS! */
    SAY AREA_DAY               @115 /* New day, new me, new tale! */
    SAY AREA_NIGHT             @116 /* This is the rhythm of the night, said once a Rashemi bard. */
    SAY SELECT_COMMON1         @117 /* Yes, darlin'? */
    SAY SELECT_COMMON2         @118 /* You want me? */
    SAY SELECT_COMMON3         @119 /* Yes, honey? */
    SAY SELECT_COMMON4         @120 /* Mhmmmmmm... */
    SAY SELECT_COMMON5         @121 /* I feel the chosen one. */
    SAY SELECT_COMMON6         @122 /* In need of my services? */
    SAY SELECT_ACTION1         @123 /* Yes, sirrr. */
    SAY SELECT_ACTION2         @124 /* On your command. */
    SAY SELECT_ACTION3         @125 /* My pleasure. */
    SAY SELECT_ACTION4         @126 /* Whatever you wish for. */
    SAY SELECT_ACTION5         @127 /* Of course! */
    SAY SELECT_ACTION6         @128 /* On my way! */
    SAY SELECT_ACTION7         @129 /* Yes, yes! */
    SAY REACT_TO_DIE_GENERAL   @130 /* No, oh Goddess, no! */
    SAY SELECT_RARE1           @131 /* My pleeeasuuure. */
    SAY SELECT_RARE2           @132 /* Ooooh yeees! */
    SAY CRITICAL_HIT           @133 /* Oh, you liked that? */
    SAY CRITICAL_MISS          @134 /* There's luck in leisure. */
    SAY TARGET_IMMUNE          @135 /* I'll find a way to harm you! */
    SAY INVENTORY_FULL         @136 /* I'm looking for a strong independent man to carry my luggage. */
    SAY PICKED_POCKET          @137 /* Opportunity makes a thief! */
    SAY HIDDEN_IN_SHADOWS      @138 /* Even though I walk through the valley of shadows I fear no evil, for my Lady is with me. */
    SAY SPELL_DISRUPTED        @139 /* C'mon, focus! */
    SAY SET_A_TRAP             @140 /* Surprise! */
  OUTER_SET file_num += 1
END

PRINT "Copied %file_num% creature files"
PRINT "Edited %file_edited_num% existing creature files"


//
// Areas
//

ACTION_DEFINE_ARRAY areas
BEGIN
  // Caly's "random" encounter are
  ~6W2000~
END

SILENT
OUTER_SET file_num = 0

ACTION_PHP_EACH areas AS num => are_file
BEGIN
  COPY ~CompassOfWomanhood/areas/caly/%are_file%.are~ ~override~
  OUTER_SET file_num += 1
END

PRINT "Copied %file_num% area files"


//
// Scripts
//

SILENT
OUTER_SET file_num = 0

WITH_TRA ~CompassOfWomanhood/translations/%LANGUAGE%/caly_tales.tra~
BEGIN
  // associative array doesn't seem to work with RESOLVE_STR_REF
  OUTER_SET 6WCALY_INIT_BANDITS_00 = RESOLVE_STR_REF(@1000100)
  OUTER_SET 6WCALY_INIT_BANDITS_01 = RESOLVE_STR_REF(@1000101)
  OUTER_SET 6WCALY_INIT_BANDITS_02 = RESOLVE_STR_REF(@1000102)
  OUTER_SET 6WCALY_INIT_BANDITS_03 = RESOLVE_STR_REF(@1000103)
END

ACTION_FOR_EACH script_file IN ~6WCAL0~  // ~6WCALY~ ~6WCAL25~
BEGIN
  COMPILE ~CompassOfWomanhood/scripts/caly/%script_file%.baf~ ~override~ EVALUATE_BUFFER
  OUTER_SET file_num += 1
END

ACTION_PHP_EACH areas AS num => script_file
BEGIN
  COMPILE ~CompassOfWomanhood/areas/caly/%script_file%.baf~ ~override~
    EVALUATE_BUFFER
  OUTER_SET file_num += 1
END

PRINT "Compiled %file_num% new scripts"


ACTION_DEFINE_ASSOCIATIVE_ARRAY script_extensions
BEGIN
  // quests
  //TODO

  // place her in the temple district
  ~CompassOfWomanhood/areas/caly/ar0900.baf~  =>  ~ar0900.bcs~

  // summon in ToB
  //TODO:
  // ~CompassOfWomanhood/areas/caly/ar4500.baf~  =>  ~ar4500.bcs~
END

SILENT
OUTER_SET file_num = 0
ACTION_PHP_EACH script_extensions AS ext => script
BEGIN
  EXTEND_BOTTOM ~%script%~ ~%ext%~
  OUTER_SET file_num += 1
END
PRINT "Extended %file_num% existing scripts"


//
// Items
//

SILENT
OUTER_SET file_num = 0

// letter (Nun's posession)
COPY ~CompassOfWomanhood/items/caly/_6WSCR00.ITM~ ~override~
  SAY NAME1 @50
  SAY NAME2 @51
  SAY UNIDENTIFIED_DESC @52
OUTER_SET file_num += 1

// handkerchief (Nun's posession)
COPY ~CompassOfWomanhood/items/caly/_6WMSC00.ITM~ ~override~
  SAY NAME1 @60
  SAY NAME2 @61
  SAY UNIDENTIFIED_DESC @62
OUTER_SET file_num += 1

PRINT "Copied %file_num% item files"

