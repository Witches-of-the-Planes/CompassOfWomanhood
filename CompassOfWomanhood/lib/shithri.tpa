//
// Main Shithri component
//


//
// Epilogues
//

SILENT
OUTER_SET file_num = 0

/* No Neera romance, visiting Chult. */
COPY ~CompassOfWomanhood/epilogues/shithri/6W_shithri.2da~ ~override/6W_shithri1.2da~
  REPLACE ~99998~ @12
  REPLACE ~99999~ @20
OUTER_SET file_num += 1

/* No Neera romance, coming back to the Moonsea. */
COPY ~CompassOfWomanhood/epilogues/shithri/6W_shithri.2da~ ~override/6W_shithri2.2da~
  REPLACE ~99998~ @12
  REPLACE ~99999~ @21
OUTER_SET file_num += 1

/* Neera romance. */
COPY ~CompassOfWomanhood/epilogues/shithri/6W_shithri.2da~ ~override/6W_shithri3.2da~
  REPLACE ~99998~ @12
  REPLACE ~99999~ @22
OUTER_SET file_num += 1

/* Neera's epilogue, if romancing Shithri */
//TODO: make sure if basing it on NeeraD2.2da is not needed.
COPY ~CompassOfWomanhood/epilogues/shithri/6W_neera.2da~ ~override/6W_neera3.2da~
  REPLACE ~99999~ @23
OUTER_SET file_num += 1

PRINT "Copied %file_num% epilogue files"

// Prepend epilogues
SILENT
EXTEND_TOP ~ar6200.bcs~ ~CompassOfWomanhood/areas/shithri/ar6200.baf~
PRINT "Extended epilogue file with new epilogues"


//
// Dialogues
//

ACTION_DEFINE_ARRAY dialogues
BEGIN
  // SoA
  ~CompassOfWomanhood/dialogues/shithri/6WSHIT.d~
  ~CompassOfWomanhood/dialogues/shithri/B6WSHIT.d~
  ~CompassOfWomanhood/dialogues/shithri/6WSHITP.d~
  ~CompassOfWomanhood/dialogues/shithri/6WSHITJ.d~
  ~CompassOfWomanhood/dialogues/shithri/6WSHITD.d~

  // ToB
  ~CompassOfWomanhood/dialogues/shithri/6WSHI25.d~
  ~CompassOfWomanhood/dialogues/shithri/6WSHI25A.d~
  ~CompassOfWomanhood/dialogues/shithri/B6WSHI25.d~
  ~CompassOfWomanhood/dialogues/shithri/6WSHI25P.d~
  ~CompassOfWomanhood/dialogues/shithri/6WSHI25J.d~
  ~CompassOfWomanhood/dialogues/shithri/6WSHI25D.d~

  // quests-related creatures
  ~CompassOfWomanhood/dialogues/shithri/6WPIRDR.d~
  ~CompassOfWomanhood/dialogues/shithri/6WDRINK.d~
END

ACTION_DEFINE_ARRAY dialogues_extra
BEGIN
  // romance & interjections
  ~CompassOfWomanhood/dialogues/shithri/bshithri_neera_romance.d~
  ~CompassOfWomanhood/dialogues/shithri/shithri_interjections.d~

  // quests
  ~CompassOfWomanhood/dialogues/shithri/shithri_brynnlaw.d~
  ~CompassOfWomanhood/dialogues/shithri/shithri_drink_quest.d~

  // player-initiated dialogues
  ~CompassOfWomanhood/dialogues/shithri/shithri_pid.d~
END

SILENT
OUTER_SET file_num = 0

ACTION_PHP_EACH dialogues AS num => dialog
BEGIN
  COMPILE ~%dialog%~ ~override~
  OUTER_SET file_num += 1
END

ACTION_PHP_EACH dialogues_extra AS num => dialog
BEGIN
  COMPILE ~%dialog%~ ~override~
  OUTER_SET file_num += 1
END

PRINT "Compile %file_num% dialog files"


//
// Game files
//

SILENT
OUTER_SET file_num = 0

APPEND ~pdialog.2da~
  ~6WSHITHRI       6WSHITP            6WSHITJ           6WSHITD~
  UNLESS ~6WSHITHRI~
  UNLESS ~25POST~

APPEND ~pdialog.2da~
  ~6WSHITHRI       6WSHITP            6WSHITJ           6WSHITD        6WSHI25P           6WSHI25J              6WSHI25D              6WSHI25~
  UNLESS ~6WSHITHRI~
  IF ~25POST~
OUTER_SET file_num += 1

APPEND ~interdia.2da~
  ~6WSHITHRI      B6WSHIT~
  UNLESS ~6WSHITHRI~
  UNLESS ~25FILE~

APPEND ~interdia.2da~
  ~6WSHITHRI      B6WSHIT       B6WSHI25~
  UNLESS ~6WSHITHRI~
  IF ~25FILE~
OUTER_SET file_num += 1

APPEND ~npclevel.2da~
  ~6WSHITHRI    6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08  6Wshit086 Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08  6Wshit086 Wshit08 6Wshit08 6Wshit086 Wshit08 6Wshit08 Wshit08~
  UNLESS ~6WSHITHRI~
OUTER_SET file_num += 1

APPEND ~npclvl25.2da~
  ~6WSHITHRI    6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08  6Wshit086 Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08   6Wshit08  6Wshit08 6Wshit08 6Wshit08 6Wshit08 6Wshit08 6Wshit08 6Wshit08~
  UNLESS ~6WSHITHRI~
OUTER_SET file_num += 1

// add resistance filters for area-type match and not-match mask - needed for Sea Shanty skill
APPEND ~splprot.2da~ ~COW_AREATYPE_MATCH_n%TAB%0x106%TAB%-1%TAB%8~
  UNLESS ~COW_AREATYPE_MATCH_n~
APPEND ~splprot.2da~ ~COW_AREATYPE_NOTMATCH_n%TAB%0x106%TAB%-1%TAB%9~
  UNLESS ~COW_AREATYPE_NOTMATCH_n~

  COPY_EXISTING ~splprot.2da~ ~override~
    COUNT_2DA_COLS no_cols
    READ_2DA_ENTRIES_NOW rows no_cols // reading into `rows` array
    FOR (row = 1; row < rows; ++row)  // iterating over indexes of `rows` array
    BEGIN
       // read the first column
       READ_2DA_ENTRY_FORMER rows row 0 ~name~
       PATCH_IF ~%name%~ STRING_EQUAL_CASE ~COW_AREATYPE_MATCH_n~ BEGIN
          SET splprot__areatype_match__num = %row%
       END
       PATCH_IF ~%name%~ STRING_EQUAL_CASE ~COW_AREATYPE_NOTMATCH_n~ BEGIN
          SET splprot__areatype_notmatch__num = %row%
       END
    END
  BUT_ONLY_IF_IT_CHANGES
OUTER_SET file_num += 1

PRINT "Extended %file_num% game scripts"


//
// Spells
//

ACTION_DEFINE_ARRAY spells
BEGIN
  // Shithri's bonus to attack number
  ~CompassOfWomanhood/spells/shithri/6WSHIEAT.spl~

  // Shithri's "Sea Shanty" ability
  ~CompassOfWomanhood/spells/shithri/6WSHANT2.spl~
  ~CompassOfWomanhood/spells/shithri/6WSHANT4.spl~

  // Bandana's spell
  ~CompassOfWomanhood/spells/shithri/6WBAND.spl~
END

SILENT
OUTER_SET file_num = 0

ACTION_PHP_EACH spells AS num => spell
BEGIN
  COPY ~%spell%~ ~override~
  OUTER_SET file_num += 1
END

// Shithri's "Sea Shanty" ability - others

// translate the string to be displayed to "Hums a sea shanty"
COPY ~CompassOfWomanhood/spells/shithri/6WSHANT1.spl~ ~override~
  // unfortunately, `LPF ALTER_EFFECT` doesn't work with translatable strings:
  //   LPF ALTER_EFFECT INT_VAR match_opcode = 139 parameter1 = @202 END
  // 0x7be is the address under which the string to be displayed
  SAY 0x7be @202 /* Hums a sea shanty */
OUTER_SET file_num += 1

// resistance / not-working in the forest needs to use the actual areatype-match code
COPY ~CompassOfWomanhood/spells/shithri/6WSHANT0.spl~ ~override~
  // unfortunately `LPF ALTER_EFFECT` doesn't seem to work fine when there are two of those
  //   LPF ALTER_EFFECT INT_VAR match_opcode = 326 parameter2 = %splprot__areatype_match__num% END

  // it's the address under which the parameter2 (of type int64), condition type for immunity, is located:
  WRITE_LONG 0x102 splprot__areatype_match__num
OUTER_SET file_num += 1

// main innate ability file
COPY ~CompassOfWomanhood/spells/shithri/6WSHANT.spl~ ~override~
  SAY NAME1 @200 /* Sea Shanty */
  SAY NAME2 @200
  SAY UNIDENTIFIED_DESC @201 /* Sea Shanty
Being an experienced sailor, Shithri can hum a simple sea shanty to stay alert whatever happens for 1 turn. While she sings, she gains +1 Luck bonus and becomes immune to sleep, fear, confusion, and stun. This ability cannot be used while inside a forest. */
  SAY IDENTIFIED_DESC   @201

  // it's the address under which the parameter2 (of type int64), condition type for immunity, is located:
  WRITE_LONG 0xa2 splprot__areatype_match__num
OUTER_SET file_num += 1

COPY ~CompassOfWomanhood/spells/shithri/6WSHANT3.spl~ ~override~
  SAY NAME1 @203 /* Stop humming */
  SAY NAME2 @203 /* Stop humming */
  SAY UNIDENTIFIED_DESC @204 /* Stop humming
Stop humming the sea shanty early. */
  SAY IDENTIFIED_DESC   @204
OUTER_SET file_num += 1


// Shithri getting completely drunk and then sobering up
COPY ~CompassOfWomanhood/spells/shithri/6WDRUNK.spl~ ~override~
  SAY NAME1 @210 /* Drunkenness */
  SAY NAME2 @210 /* Drunkenness */
OUTER_SET file_num += 1
COPY ~CompassOfWomanhood/spells/shithri/6WDRUNK0.spl~ ~override~
  SAY NAME1 @211 /* Sobering up */
  SAY NAME2 @211 /* Sobering up */
OUTER_SET file_num += 1

PRINT "Copied %file_num% spell files"



//
// Sounds
//

// `LAF HANDLE_AUDIO` doesn't seem to support %LANGUAGE% placeholder
/*
LAF HANDLE_AUDIO
  STR_VAR
    oggdec_path = ~CompassOfWomanhood/sounds~
    audio_path  = ~CompassOfWomanhood/translations/%LANGUAGE%/sounds/shithri~
END
*/

// US English is the base
SILENT
OUTER_SET file_num = 0
ACTION_BASH_FOR ~CompassOfWomanhood/translations/en_US/sounds/shithri~ ~^B?6W.*\.wav$~
BEGIN
  COPY ~%BASH_FOR_FILESPEC%~ ~override~
  OUTER_SET file_num += 1
END
PRINT "Copied %file_num% sound files for en_US"

// Other languages may add or overwrite some sounds.
ACTION_IF !(%LANGUAGE% STRING_EQUAL "en_US") THEN
BEGIN
  SILENT
  OUTER_SET file_num = 0
  ACTION_BASH_FOR ~CompassOfWomanhood/translations/%LANGUAGE%/sounds/shithri~ ~^B?6W.*\.wav$~
  BEGIN
    COPY ~%BASH_FOR_FILESPEC%~ ~override~
    OUTER_SET file_num += 1
  END
  PRINT "Copied %file_num% sound files for %LANGUAGE%"
END

//
// Companion herself
//

// portraits
SILENT
COPY ~CompassOfWomanhood/creatures/shithri/6WshithS.bmp~ ~override~
COPY ~CompassOfWomanhood/creatures/shithri/6WshithM.bmp~ ~override~
COPY ~CompassOfWomanhood/creatures/shithri/6WshithL.bmp~ ~override~
PRINT "Copied 3 portrait files"

// creature
SILENT
OUTER_SET file_num = 0
OUTER_SET file_edited_num = 0

OUTER_SET 6W_SHITHRI_NAME = RESOLVE_STR_REF(@10)

ACTION_FOR_EACH cre_file IN ~6WSHIT08~ ~6WSHIT09~ ~6WSHIT10~ ~6WSHIT11~ ~6WSHIT13~ ~6WSHIT15~ ~6WSHIT25~
BEGIN
  COPY ~CompassOfWomanhood/creatures/shithri/%cre_file%.cre~ ~override~
    WRITE_LONG NAME1 %6W_SHITHRI_NAME%
    WRITE_LONG NAME2 %6W_SHITHRI_NAME%
    SAY BIO   @12
  
    SAY HAPPY                  @100 /* Ho! 'tis the hearties I be lookin' fer! */
    SAY UNHAPPY_ANNOYED        @101 /* I hope ye jus' jestin'. */
    SAY UNHAPPY_SERIOUS        @102 /* I nah so glad fer this capt'n o' ours. */
    SAY UNHAPPY_BREAKING_POINT @103 /* O'er me dead body! */
    SAY LEADER                 @104 /* This bosun shall pull all the strings! */
    SAY TIRED                  @105 /* Even the most darin' bucko can't stay on her watch all time. */
    SAY BORED                  @106 /* Ayyy... Idlin' be tiresome. This bucko nah used t' doin' nothin'. */
    SAY BATTLE_CRY1            @107 /* Blow 'em down! */
    SAY BATTLE_CRY2            @108 /* Draught fer me blade! */
    SAY BATTLE_CRY3            @109 /* Ye'll make nice salmagundi! */
    SAY HURT                   @110 /* Takes more than that t' blow down this bucko! */
    SAY DYING                  @111 /* Feel pity fer the devils. I be comin'. */
    SAY AREA_FOREST            @112 /* Sssh. I be listenin' t' Mother's voice. */
    SAY AREA_CITY              @113 /* City means tavern. Tavern means draught. */
    SAY AREA_DUNGEON           @114 /* Some treasure t' find in 'ere, I hope? */
    SAY AREA_DAY               @115 /* Sun in the sky, it be new day. Another roddle diddle one, mayhaps? */
    SAY AREA_NIGHT             @116 /* Wha' a fine night fer a draught... aye? */
    SAY SELECT_COMMON1         @117 /* Aye? */
    SAY SELECT_COMMON2         @118 /* Wha'? */
    SAY SELECT_COMMON3         @119 /* Awaitin' command. */
    SAY SELECT_COMMON4         @120 /* This bucko be ready, me capt'n. */
    SAY SELECT_COMMON5         @121 /* Do speak. */
    SAY SELECT_COMMON6         @122 /* Careful ye. */
    SAY SELECT_ACTION1         @123 /* Aye aye! */
    SAY SELECT_ACTION2         @124 /* Roger that. */
    SAY SELECT_ACTION3         @125 /* It be done. */
    SAY SELECT_ACTION4         @126 /* It be breeze. */
    SAY SELECT_ACTION5         @127 /* Clear as day. */
    SAY SELECT_ACTION6         @128 /* Will do. */
    SAY SELECT_ACTION7         @129 /* Oh capt'n, me capt'n. */
    SAY REACT_TO_DIE_GENERAL   @130 /* Yer death shall nah stay unavenged. */
    SAY SELECT_RARE1           @131 /* Care fer a draught? Nay? So why naggin'? */
    SAY SELECT_RARE2           @132 /* Wha' do ye know? Land-lubber... */
    SAY CRITICAL_HIT           @133 /* Yarrr! */
    SAY CRITICAL_MISS          @134 /* Blimey, not even close! */
    SAY TARGET_IMMUNE          @135 /* This nah work. */
    SAY INVENTORY_FULL         @136 /* I be a bucko, nah a ship. Can't carry that much. */
    SAY PICKED_POCKET          @137 /* Jus' took a thing or two. */
    SAY HIDDEN_IN_SHADOWS      @138 /* No flag raised. */
    SAY SPELL_DISRUPTED        @139 /* Sink me, that go nah good... */
    SAY SET_A_TRAP             @140 /* Lil something. Big surprise. */
  OUTER_SET file_num += 1
END


// Implementation Note:
// In the original game, some creatures lack Script Identifiers. Those are needed in order to refer
// to a creature inside scripts. In particular - to check if it's alive, in your location, move it
// around etc.
//
// This mod adds Script Identifiers to some creatures that lack them. These Script Identifiers match
// the creature's file name.
//
// While name collisions could theoretically happen, it is assumed that modders don't give their
// creatures Script Identifiers that match existing file names.

// TODO: write a more general function, that could calculate the data automatically?
// ~THUMB~ ~SBCUST01~ ~SBCUST02~ ~SBWENCH~ ~SBSLUM01~
COPY_EXISTING ~THUMB.cre~ ~override~
  PATCH_IF (LONG_AT 0x280) == 0  ||  (LONG_AT 0x280) == 0x656e6f4e // `None`
  BEGIN
    WRITE_LONG 0x280 0x4d554854
    WRITE_LONG 0x284 0x00000042
    INNER_ACTION
    BEGIN
      OUTER_SET file_edited_num += 1
    END
  END
  BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~SBCUST01.cre~ ~override~
  PATCH_IF (LONG_AT 0x280) == 0  ||  (LONG_AT 0x280) == 0x656e6f4e // `None`
  BEGIN
    WRITE_LONG 0x280 0x55434253
    WRITE_LONG 0x284 0x31305453
    INNER_ACTION
    BEGIN
      OUTER_SET file_edited_num += 1
    END
  END
  BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~SBCUST02.cre~ ~override~
  PATCH_IF (LONG_AT 0x280) == 0  ||  (LONG_AT 0x280) == 0x656e6f4e // `None`
  BEGIN
    WRITE_LONG 0x280 0x55434253
    WRITE_LONG 0x284 0x32305453
    INNER_ACTION
    BEGIN
      OUTER_SET file_edited_num += 1
    END
  END
  BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~SBWENCH.cre~ ~override~
  PATCH_IF (LONG_AT 0x280) == 0  ||  (LONG_AT 0x280) == 0x656e6f4e // `None`
  BEGIN
    WRITE_LONG 0x280 0x45574253
    WRITE_LONG 0x284 0x0048434e
    INNER_ACTION
    BEGIN
      OUTER_SET file_edited_num += 1
    END
  END
  BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~SBSLUM01.cre~ ~override~
  PATCH_IF (LONG_AT 0x280) == 0  ||  (LONG_AT 0x280) == 0x656e6f4e // `None`
  BEGIN
    WRITE_LONG 0x280 0x4c534253
    WRITE_LONG 0x284 0x31304d55
    INNER_ACTION
    BEGIN
      OUTER_SET file_edited_num += 1
    END
  END
  BUT_ONLY_IF_IT_CHANGES
COPY_EXISTING ~SBSLUM02.cre~ ~override~
  PATCH_IF (LONG_AT 0x280) == 0  ||  (LONG_AT 0x280) == 0x656e6f4e // `None`
  BEGIN
    WRITE_LONG 0x280 0x4c534253
    WRITE_LONG 0x284 0x32304d55
    INNER_ACTION
    BEGIN
      OUTER_SET file_edited_num += 1
    END
  END
  BUT_ONLY_IF_IT_CHANGES



// new creature: drinking sailor
COPY ~CompassOfWomanhood/creatures/shithri/6WPIRDR.cre~ ~override~
  SAY NAME1 @1000
  SAY NAME2 @1000
OUTER_SET file_num += 1

PRINT "Copied %file_num% creature files"
PRINT "Edited %file_edited_num% existing creature files"


//
// Scripts
//

SILENT
OUTER_SET file_num = 0

ACTION_FOR_EACH script_file IN ~6WSHITH~ ~6WSHI25~ ~6WPIRDR~ ~6WSHITP~ ~6WNEERP~
BEGIN
  COMPILE ~CompassOfWomanhood/scripts/shithri/%script_file%.baf~ ~override~
  OUTER_SET file_num += 1
END
PRINT "Compiled %file_num% new scripts"

ACTION_DEFINE_ASSOCIATIVE_ARRAY script_extensions
BEGIN
  // quests
  ~CompassOfWomanhood/scripts/shithri/shithri_brynnlaw.baf~         =>  ~6WSHITH.bcs~
  ~CompassOfWomanhood/scripts/shithri/shithri_drink_quest.baf~      =>  ~6WSHITH.bcs~
  ~CompassOfWomanhood/areas/shithri/ar0313.baf~                     =>  ~ar0313.bcs~

  // Dorn sparring
  ~CompassOfWomanhood/scripts/shithri/shithri_dorn25.baf~           =>  ~dorn25.bcs~

  // Jan Jansen joining
  ~CompassOfWomanhood/scripts/shithri/shithri_jan.baf~              =>  ~jan.bcs~

  // Neera romance
  ~CompassOfWomanhood/scripts/shithri/bshithri_neera_romance.baf~   =>  ~6WSHITH.bcs~
  ~CompassOfWomanhood/scripts/shithri/bshith25_neera_romance.baf~   =>  ~6WSHI25.bcs~ 
  ~CompassOfWomanhood/scripts/shithri/shithri_bneera_romance.baf~   =>  ~neera.bcs~
  ~CompassOfWomanhood/scripts/shithri/shithri_bneer25_romance.baf~  =>  ~neer25.bcs~

  // place her in the docks
  ~CompassOfWomanhood/areas/shithri/ar0300.baf~  =>  ~ar0300.bcs~

  // summon in ToB
  ~CompassOfWomanhood/areas/shithri/ar4500.baf~  =>  ~ar4500.bcs~

  // cursed bandana script
  ~CompassOfWomanhood/items/shithri/_6WBAN02_baldur.baf~  =>  ~baldur.bcs~
END

SILENT
OUTER_SET file_num = 0
ACTION_PHP_EACH script_extensions AS ext => script
BEGIN
  EXTEND_BOTTOM ~%script%~ ~%ext%~
  OUTER_SET file_num += 1
END
PRINT "Extended %file_num% existing scripts"


//
// Items
//

ACTION_DEFINE_ARRAY item_icons
BEGIN
  ~CompassOfWomanhood/items/shithri/6WSAB01I.BAM~
  ~CompassOfWomanhood/items/shithri/6WSAB01D.BAM~
END

SILENT
OUTER_SET file_num = 0
ACTION_PHP_EACH item_icons AS num => icon
BEGIN
  COPY ~%icon%~ ~override~
  OUTER_SET file_num += 1
END

ACTION_BASH_FOR ~CompassOfWomanhood/items/shithri~ ~6WDR.*\.BAM~
BEGIN
  COPY ~%BASH_FOR_FILESPEC%~ ~override~
  OUTER_SET file_num += 1
END

PRINT "Copied %file_num% item icons"

SILENT
OUTER_SET file_num = 0

// cutlass
COPY ~CompassOfWomanhood/items/shithri/_6WSAB01.ITM~ ~override~
  SAY NAME1 @50
  SAY NAME2 @51
  SAY UNIDENTIFIED_DESC @52
OUTER_SET file_num += 1

// bandana
COPY ~CompassOfWomanhood/items/shithri/_6WBAN01.ITM~ ~override~
  SAY NAME1 @60
  SAY NAME2 @61
  SAY UNIDENTIFIED_DESC @62
  SAY IDENTIFIED_DESC   @63
COPY ~CompassOfWomanhood/items/shithri/_6WBAN02.ITM~ ~override~
  SAY NAME1 @60
  SAY NAME2 @61
  SAY UNIDENTIFIED_DESC @62
  SAY IDENTIFIED_DESC   @63
OUTER_SET file_num += 1

// Shithri's drink
COPY ~CompassOfWomanhood/items/shithri/_6WDRINK.itm~ ~override~
  SAY NAME1 @70
  SAY NAME2 @71
  SAY UNIDENTIFIED_DESC @72
OUTER_SET file_num += 1

OUTER_SET 6W_SHITHRI_DRINK_REMOVE = RESOLVE_STR_REF(@75)
APPEND item_use.2da
  ~_6WDRINK 6WSHITHRI %6W_SHITHRI_DRINK_REMOVE% 3 %6W_SHITHRI_NAME%~
  UNLESS ~_6WDRINK~

// liquors
// (mapping two-digit midfix to string number)
//TODO: icons
ACTION_DEFINE_ASSOCIATIVE_ARRAY drink_items
BEGIN
  ~00~  => 4000000  /* Bitter Black Ale */
  ~01~  => 4010000  /* Shadowdark Ale */
  ~02~  => 4020000  /* Suz-Ale */
  ~03a~ => 4030000  /* Golden Sands Basic */
  ~03b~ => 4030100  /* Golden Sands Gold */
  ~03c~ => 4030200  /* Golden Sands Orange */
  ~04~  => 4040000  /* Iriaeboran North Brew */
  ~05~  => 4050000  /* Luiren's Best Ale */
  ~06~  => 4060000  /* Tanagyr's Ale */
  ~07~  => 4070000  /* Undermountain Alurlyath */
  ~08~  => 4080000  /* Arabellan Dry */
  ~09~  => 4090000  /* Evermead */
  ~10~  => 4100000  /* Westgate Ruby */
  ~11~  => 4110000  /* Saerloonian Topaz */
  ~12~  => 4120000  /* Berduskan Dark */
  ~13~  => 4130000  /* Winter Wine */
  ~14~  => 4140000  /* Dragon's Beer */
  ~20~  => 4200000  /* Turnip Nalewka */
  ~21~  => 4210000  /* Halfling's Help */
  ~90~  => 4900000  /* Elminster's Choice */
END

WITH_TRA ~CompassOfWomanhood/translations/%LANGUAGE%/shithri_drink_quest.tra~ 
BEGIN
  ACTION_PHP_EACH drink_items AS midfix => num
  BEGIN
    COPY ~CompassOfWomanhood/items/shithri/_6WDR00.itm~ ~override/_6WDR%midfix%.itm~
      SAY NAME1 (AT "num")
      SAY NAME2 (AT "num")
      num += 1
      SAY UNIDENTIFIED_DESC (AT "num")
      SAY IDENTIFIED_DESC   (AT "num")

      SPRINT icon_filename "6WDR%midfix%I"
      PATCH_IF FILE_EXISTS_IN_GAME "%icon_filename%.BAM" BEGIN
        WRITE_EVALUATED_ASCII 0x3a "%icon_filename%" #8
      END

    EXTEND_BOTTOM ~6WSHITH.bcs~ ~CompassOfWomanhood/scripts/shithri/shithri_drink_quest_iter.baf~
      SPRINT drink_num "%midfix%"
      EVALUATE_BUFFER

    OUTER_SET file_num += 1
  END
END

PRINT "Copied %file_num% item files"


//
// Journal entries
//

SILENT
OUTER_SET file_num = 0

ADD_JOURNAL MANAGED
  @9000000 @9000001 @9000002 @9000003 @9000004 @9000005
  @9000010 @9000011 @9000012 @9000013
  @9000020
  USING ~CompassOfWomanhood/translations/%LANGUAGE%/shithri_brynnlaw.tra~
OUTER_SET file_num += 11

ADD_JOURNAL MANAGED
  @9000000
  @9000010 @9000011 @9000012 @9000015 @9000016
  @9000020
  @9000030 @9000031
  @9000040 @9000041 @9000042 @9000043
  USING ~CompassOfWomanhood/translations/%LANGUAGE%/shithri_drink_quest.tra~
OUTER_SET file_num += 1

PRINT "Added %file_num% journal entries"
